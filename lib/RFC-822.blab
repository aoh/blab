##
## STANDARD FOR THE FORMAT OF ARPA INTERNET TEXT MESSAGES, RFC-822 (many definitions of MIME rely on this)
##

RFC-822 {

   address     =  mailbox | group
   addr-spec   =  local-part "@" domain
   ALPHA       =  [A-Za-z]
   atom        =  [a-z]* # FIXME, 1*<any CHAR except specials, SPACE and CTLs>
   authentic   =   "From"       ":"   mailbox
                 | ( "Sender"     ":"   mailbox
                     "From"       ":" mailbox) # what was #mailbox?
   CHAR        =  [\x00-\x7f]    #  <any ASCII character> 
   comment     =  "(" (ctext | quoted-pair | comment)* ")"
   CR          =  13
   CRLF        =  13 10
   ctext       =  [a-z] # FIXME, <any CHAR excluding "(", ")", "\" & CR, & including linear-white-space>
   CTL         =  [\x00-\x1f\x7f] # <any ASCII control character and DEL>
   DIGIT2      = DIGIT DIGIT
   date        =  DIGIT2+ month DIGIT2
   dates       =   orig-date ("" | resent-date )
   date-time   =  ("" |  day "," ) date time
   day         =  "Mon"  | "Tue" |  "Wed"  | "Thu"
               |  "Fri"  | "Sat" |  "Sun"
   delimiters  =  specials | linear-white-space | comment
   destination =  "To"          ":" address
               |  "Resent-To"   ":" address
               |  "cc"          ":" address
               |  "Resent-cc"   ":" address
               |  "bcc"         ":" address
               |  "Resent-bcc"  ":" address
   DIGIT       =  [0-9]
   domain      =  sub-domain ("." sub-domain)*
   domain-literal =  "[" (dtext | quoted-pair)* "]"
   domain-ref  =  atom
   dtext       =  [a-z] # FIXME, <any CHAR excluding "[", "]", "\" & CR, & including linear-white-space>
   extension-field = "extension-field" # <Any field which is defined in a document published as a formal extension to this specification; none will have names beginning with the string "X-">
   field       =  field-name ":" ("" | field-body ) CRLF
   fields      =  dates source destination+ optional-field*
   field-body  =  field-body-contents ("" | CRLF LWSP-char field-body)
   field-body-contents = [a-z]+ # FIXME <the ASCII characters making up the field-body, as defined in the following sections, and consisting of combinations of atom, quoted-string, and specials tokens, or else consisting of texts>
   field-name  =  CHAR+ # FIXME, 1*<any CHAR, excluding CTLs, SPACE, and ":">
   group       =  phrase ":" ("" | mailbox) ";"
   hour        =  DIGIT2 ":" DIGIT2 ("" | ":" DIGIT2)
   HTAB        =  9 
   LF          =  10
   linear-white-space =  (("" | CRLF) LWSP-char)+
   local-part  =  word ("." word)*
   LWSP-char   =  SPACE | HTAB
   mailbox     =  addr-spec |  phrase route-addr
   message     =  fields ( CRLF text* )*
   month       =  "Jan"  |  "Feb" |  "Mar"  |  "Apr"
               |  "May"  |  "Jun" |  "Jul"  |  "Aug"
               |  "Sep"  |  "Oct" |  "Nov"  |  "Dec"
   msg-id      =  "<" addr-spec ">"
   optional-field 
                 =  "Message-ID"        ":"   msg-id
                 |  "Resent-Message-ID" ":"   msg-id
                 |  "In-Reply-To"       ":"  (phrase | msg-id)*
                 |  "References"        ":"  (phrase | msg-id)*
                 |  "Keywords"          ":"  phrase
                 |  "Subject"           ":"  text*
                 |  "Comments"          ":"  text*
                 |  "Encrypted"         ":" word+
                 |  extension-field
                 |  user-defined-field
   orig-date   =  "Date"        ":"   date-time
   originator  =   authentic ("" | "Reply-To"   ":" address)
   phrase      =  word+
     
   qtext       =  CHAR # FIXME, <any CHAR excepting <">, "\" & CR, and including linear-white-space>
   quoted-pair =  "\\" CHAR
   quoted-string = "\"" (qtext | quoted-pair)* "\""
   received    =  "Received"    ":"
                    ("" | "from" domain)
                    ("" | "by"   domain)
                    ("" | "via"  atom)
                    ("" | ("with" atom)*)
                    ("" | "id"   msg-id)
                    ("" | "for"  addr-spec)
                    ";"    date-time
     
   resent      =   resent-authentic
                   ("" | "Resent-Reply-To"  ":" address+)
   resent-authentic 
                 =   "Resent-From"      ":"   mailbox
                 | ( "Resent-Sender"    ":"   mailbox
                     "Resent-From"      ":" mailbox+  )
   resent-date =  "Resent-Date" ":"   date-time
   return      =  "Return-path" ":" route-addr
   route       =  ("@" domain)+ ":"
   route-addr  =  "<" ("" | route) addr-spec ">"
   source      = ("" | trace ) originator ("" | resent )
   SPACE       =  32
   specials    =  "(" | ")" | "<" | ">" | "@"
               |  "," | ";" | ":" | "\\" | "\""
               |  "." | "[" | "]"
   sub-domain  =  domain-ref | domain-literal
   text        = [a-z] # FIXME,  <any CHAR, including bare CR & bare LF, but NOT including CRLF>
   time        =  hour zone
   trace       =  return received+
   user-defined-field = [-a-z]+ # FIXME
   word        =  atom | quoted-string
   zone        =  "UT"  | "GMT"
               |  "EST" | "EDT"
               |  "CST" | "CDT"
               |  "MST" | "MDT"
               |  "PST" | "PDT"
               |  ALPHA
}
